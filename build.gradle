apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'about'
apply plugin: 'artifactory'
apply from:   'file:groovydoc.gradle'
apply from:   'file:mavenCentral.gradle'

defaultTasks 'clean', 'codenarc', 'build', 'about', 'install'


group                    = 'com.github.goldin'
ext.codenarcRuleSetFiles = [ 'codenarc.groovy' ]
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/dev/src/main/groovy/CodeNarc.gradle'


buildscript {
    repositories {
//        maven   { url "${ System.properties[ 'user.home' ] }/.m2/repository" }
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
        mavenRepo url: 'http://repo.jfrog.org/artifactory/gradle-plugins/'
    }
    // http://wiki.jfrog.org/confluence/display/RTF/Gradle+Artifactory+Plugin
    dependencies {
        classpath 'com.github.goldin.plugins:gradle:0.1.2',
                  'org.jfrog.buildinfo:build-info-extractor-gradle:2.0.14'
    }
}


repositories { mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/' }
about        { dumpDependencies = true }


dependencies {
    groovy      'org.codehaus.groovy:groovy-all:1.8.6'
    compile   ( 'org.spockframework:spock-core:0.6-groovy-1.8' ) { exclude group: 'org.codehaus.groovy' }
    compile     'org.gcontracts:gcontracts-core:1.2.5',
                'org.slf4j:slf4j-api:1.6.4'
    testRuntime 'ch.qos.logback:logback-classic:1.0.4'
}


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


idea.module {
    downloadSources = true
    downloadJavadoc = true
}


task wrapper( type: Wrapper ) { gradleVersion = '1.0-rc-3' }


/**
 * http://wiki.jfrog.org/confluence/display/RTF/Gradle+Artifactory+Plugin
 */
artifactory {
    contextUrl = 'http://evgenyg.artifactoryonline.com/evgenyg/'
    publish {
        repository {
            repoKey  = 'libs-releases-local'
            username = 'deployer'
            password = ''
        }
    }
}

/**
 * Adding sources
 */
task sourcesJar( type: Jar, dependsOn: classes ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts { archives sourcesJar }
