apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'


defaultTasks 'clean', 'codenarc', 'build', 'install'

group = 'com.goldin'

codenarcRuleSetFiles = [ 'codenarc.groovy' ]
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/dev/src/main/groovy/CodeNarc.gradle'


repositories {
	mavenRepo url: 'http://m2repo.spockframework.org/snapshots/'
	mavenCentral()
}


dependencies {
	groovy  'org.codehaus.groovy:groovy:1.8.2' // GContracts '1.2.4' only works with Groovy 1.8.2
	compile ( 'org.spockframework:spock-core:0.6-groovy-1.8-SNAPSHOT' ) { exclude group: 'org.codehaus.groovy' }
    compile 'org.gcontracts:gcontracts-core:1.2.4',
            'org.slf4j:slf4j-api:1.6.4'
}


idea.project {
    jdkName = '1.6.0_29'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


idea.module {
    downloadSources = true
    downloadJavadoc = true
}


task wrapper( type: Wrapper ) { gradleVersion = '1.0-milestone-6' }


/**
 * Adding sources artifact
 */
task sourcesJar( type: Jar, dependsOn: classes ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts { archives sourcesJar }
