apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'about'


defaultTasks 'clean', 'codenarc', 'build', 'about', 'install'


group                    = 'com.github.goldin'
ext.codenarcRuleSetFiles = [ 'codenarc.groovy' ]
apply from: 'https://raw.github.com/evgeny-goldin/gradle-plugins/dev/src/main/groovy/CodeNarc.gradle'


buildscript {
    repositories {
//        maven   { url "${ System.properties[ 'user.home' ] }/.m2/repository" }
        mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
    }
    dependencies { classpath "com.github.goldin.plugins:gradle:0.1.2" }
}


repositories { mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/' }
about        { dumpDependencies = true }


dependencies {
    groovy      'org.codehaus.groovy:groovy-all:1.8.6'
    compile   ( 'org.spockframework:spock-core:0.6-groovy-1.8' ) { exclude group: 'org.codehaus.groovy' }
    compile     'org.gcontracts:gcontracts-core:1.2.5',
                'org.slf4j:slf4j-api:1.6.4'
    testRuntime 'ch.qos.logback:logback-classic:1.0.3'
}


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}


idea.module {
    downloadSources = true
    downloadJavadoc = true
}


task wrapper( type: Wrapper ) { gradleVersion = '1.0-rc-3' }


/**
 * Adding sources
 */
task sourcesJar( type: Jar, dependsOn: classes ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts { archives sourcesJar }
